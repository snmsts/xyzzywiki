;;; -*- Mode: Lisp; Package: EDITOR -*-
;;;
;;; Last updated: <2004/12/19 10:46:48 +0900>
;;;
;;; my-bookmark.l

(provide "my-bookmark")

(in-package "editor")

(export '(bookmark-jump-to-bookmark bookmark-add-new-bookmark
	  bookmark-clear-this-bookmark bookmark-clear-all
	  my-bookmark-menu bookmark-edit-bookmark-list
	  bookmark-jump-to-previous-bookmark bookmark-jump-to-next-bookmark
	  bookmark-select-list-file bookmark-menu-selected-file
	  my-bookmark-tool-bar my-bookmark-uninstall
	  *bookmark-max* *bookmark-list* *bookmark-list-file*
	  *bookmark-filename-length* *bookmark-string-length*
	  *bookmark-list-file-list*))

;;; カスタマイズできる変数
(defvar *bookmark-menu-item-length* 16)	 	 ;; 引き出しに入れる個数
(defvar *bookmark-max* 400)	 	 	 ;; bookmark をいくつまで認めるか
(defvar *bookmark-filename-length* 24)	  	 ;; メニューに表示するファイル名の長さ（バイト数）
(defvar *bookmark-string-length* 36)	 	 ;; メニューに表示する文字列の長さ（バイト数）

(define-history-variable *bookmark-list-file* "~/bookmarklist")	 ;; しおりを保存するファイル
(defvar *bookmark-list* nil)
(defvar *bookmark-list-file-list* nil)

;;; ジャンプ
(defun bookmark-jump-to-bookmark (key &optional mn n)
  (unless mn (bookmark-data-read-from-file))
  (setq *bookmark-list* (nsubst nil t *bookmark-list*))
  (let ((fn-or-bn (car key))
	(pt (cdr key)) predt pos key1)
    (cond ((find-buffer fn-or-bn)
	   (switch-to-buffer fn-or-bn))
	  ((and (string-match "^.+/" fn-or-bn)
		(file-exist-p fn-or-bn))
	   (find-file fn-or-bn))
	  (t
	   (setq predt (if n
			   #'string<
			 #'string>))
	   (setq *bookmark-list* (sort *bookmark-list* predt :key #'cadar))
	   (setq pos (position
		      (rassoc key *bookmark-list* :test #'equal) *bookmark-list*))
	   (or
	    (setq key1 (cdr (nth (+ pos 1) *bookmark-list*)))
	    (< (- pos 1) 0)
	    (setq key1 (cdr (nth (- pos 1) *bookmark-list*))))
	   (bookmark-data-read-from-file)
	   (setq *bookmark-list* (nsubst nil t *bookmark-list*))
	   (when key1
	     (setf (cddar (rassoc key1 *bookmark-list* :test #'equal)) t))
	   (bookmark-clear-this-bookmark key "jp")
	   (plain-error "しおりを削除しました")
	   (return-from bookmark-jump-to-bookmark)))
    (when (pre-selection-p) (stop-selection))
    (goto-char pt)
    (mapc #'(lambda (l) (when (equal key (cdr l))
			  (setf (caaar l) (current-line-number))
			  (setf (cddar l) t))) *bookmark-list*)
    (bookmark-data-write-to-file *bookmark-list*)))

;;; リストの操作

;; utility

(defun bookmark-data-read-from-file ()  ;NetInstaller を参考にしました
  (with-open-file (s
		   *bookmark-list-file*
		   :direction :input
		   :if-does-not-exist :create)
    (setq *bookmark-list* (read s nil))
    ))

(defun bookmark-data-write-to-file (data)
  (with-open-file (s
		   *bookmark-list-file*
		   :direction :output
		   :if-exists :overwrite
		   :if-does-not-exist :create)
    (format s "~S" data))
  t)

(defun bookmark-get-string-from-line ()
  (let ((str (string-left-trim '(#\SPC #\TAB #\　)
			       (if (pre-selection-p)
				   (buffer-substring (selection-point) (selection-mark))
				 (buffer-substring
				  (save-excursion (goto-bol) (point))
				  (save-excursion (goto-eol) (point)))))))
    (setq str (bookmark-shorten-str str *bookmark-string-length*))
    str))

(defun bookmark-shorten-str (str lgt)
  (let ((convstr (convert-encoding-to-internal
		  *encoding-binary* str)))
    (unless (< (length convstr) lgt)
      (setq str (convert-encoding-to-internal
		 *encoding-sjis*
		 (subseq convstr 0 (1+ lgt))))
      (setq str (subseq str 0 (1- (length str)))))
    str))

;; 追加
(defun bookmark-add-new-bookmark (mn)
  (interactive)
  (unless mn (bookmark-data-read-from-file))
  (let* ((fn (get-buffer-file-name))
	 (fn-or-bn (if fn fn (buffer-name (selected-buffer))))
	 (bookmark (acons
		    (cons (current-line-number) (bookmark-get-string-from-line))
		    (cons (format-date-string "%y/%m/%d %H:%M:%S") t)
		    (cons fn-or-bn (point)))))
    (bookmark-clear-this-bookmark (cdr bookmark) "ad")
    (setq *bookmark-list* (nsubst nil t *bookmark-list*))
    (push bookmark *bookmark-list*)
    (let ((exceed (- (length *bookmark-list*) *bookmark-max*)))
      (when (> exceed 0)
	(setq *bookmark-list* (nbutlast *bookmark-list* exceed))))
    (bookmark-data-write-to-file *bookmark-list*)
    (message "しおりを挟みました")))

;; 削除
(defun bookmark-clear-this-bookmark (key cs)
  (let ((dl (rassoc key *bookmark-list* :test #'equal)))
    (setq *bookmark-list* (delete dl *bookmark-list* :test #'equal)))
  (cond ((string= cs "mn")
	 (bookmark-data-write-to-file *bookmark-list*)
	 (my-bookmark-menu t nil))
	((string= cs "jp")
	 (bookmark-data-write-to-file *bookmark-list*)))
  t)

(defun bookmark-clear-all ()
  (interactive)
  (setq *bookmark-list* nil)
  (bookmark-data-write-to-file *bookmark-list*)
  (message "すべて削除しました")
  t)

;; ソート
(defun bookmark-sort-bookmark-list-by-filename (predp predf dmode)
  (setq *bookmark-list* (sort *bookmark-list* predp :key #'cddr))
  (setq *bookmark-list* (sort *bookmark-list* predf :key #'cadr))
  (bookmark-data-write-to-file *bookmark-list*)
  (my-bookmark-menu dmode nil))

(defun bookmark-sort-bookmark-list-by-time (predt dmode)
  (setq *bookmark-list* (sort *bookmark-list* predt :key #'cadar))
  (bookmark-data-write-to-file *bookmark-list*)
  (my-bookmark-menu dmode nil))

;;; 編集
(defvar *bookmark-edit-mode-keymap* nil)
(unless *bookmark-edit-mode-keymap*
  (setq *bookmark-edit-mode-keymap* (copy-keymap *lisp-mode-map*))
  (define-key *bookmark-edit-mode-keymap* #\C-g 'bookmark-save-bmlist))

(defun bookmark-edit-bookmark-list ()
  (interactive)
  (bookmark-data-read-from-file)
  (message "#\C-g で保存終了")
  (set-buffer (get-buffer-create "*bookmark-edit*"))
  (make-local-variable 'kept-undo-information)
  (setq kept-undo-information t)
  (use-keymap *bookmark-edit-mode-keymap*)
  (insert "(\n")
  (with-output-to-buffer ("*bookmark-edit*" (point-max))
    (dolist (x (reverse *bookmark-list*))
      (format t "~S~%" x)))
  (goto-char (point-max))
  (insert ")"))

(defun bookmark-save-bmlist ()
  (interactive)
  (message "変更を保存しました")
  (set-buffer (find-buffer "*bookmark-edit*"))
  (goto-char (point-min))
  (replace-buffer "$" "" :regexp t)
  (goto-char (point-min))
  (setq *bookmark-list*
	(with-input-from-selected-buffer (read)))
  (bookmark-data-write-to-file (reverse *bookmark-list*))
  (delete-buffer (selected-buffer)))

;;; メニュ
(defun my-bookmark-menu (dmode btn)
  (interactive)
  (continue-pre-selection)
  (let ((exis (bookmark-data-read-from-file))
	(menu (create-popup-menu))
	(l (reverse *bookmark-list*)) minit sinit)
    (if exis
	(unless (< 1 (length *bookmark-list*)) (setq sinit :disable))
      (progn (setq minit :disable)
	(setq sinit :disable)
	(setq dmode nil)))
    (dotimes (i (floor (length l) *bookmark-menu-item-length*))
      (let ((popup (create-popup-menu)))
	(add-popup-menu menu popup (format nil " Bookmark-~d" i))
	(dotimes (i *bookmark-menu-item-length*)
	  (let* ((key (cdar l))
		 (fn-or-bn (cadar l))
		 (lnum (caaaar l))
		 (str (cdaaar l))
		 (jflg (cddaar l))
		 (fbn (file-namestring fn-or-bn)) menu-item)
	    (setq fbn (bookmark-shorten-str fbn *bookmark-filename-length*))
	    (setq menu-item (concat fbn " \t｜" (format nil "~A" lnum) "｜" str))
	    (add-menu-item popup nil (format nil "~A&~D: ~a"
					     (if (>= i 10)
						 (truncate i 10)
					       " ")
					     (rem i 10)
					     menu-item)
			   #'(lambda ()
			       (interactive)
			       (if dmode
				   (bookmark-clear-this-bookmark key "mn")
				 (bookmark-jump-to-bookmark key t)))
			   #'(lambda ()
			       (when jflg :check)))
	    (pop l)))))
    (let ((c 0))
      (dolist (b l)
	(let* ((key (cdr b))
	       (fn-or-bn (cadr b))
	       (lnum (caaar b))
	       (str (cdaar b))
	       (jflg (cddar b))
	       (fbn (file-namestring fn-or-bn)) menu-item)
	  (setq fbn (bookmark-shorten-str fbn *bookmark-filename-length*))
	  (setq menu-item (concat fbn " \t｜" (format nil "~A" lnum) "｜" str))
	  (add-menu-item menu nil (format nil "~A&~D: ~a"
					  (if (>= c 10)
					      (truncate c 10)
					    " ")
					  (rem c 10)
					  menu-item)
			 #'(lambda ()
			     (interactive)
			     (if dmode
				 (bookmark-clear-this-bookmark key "mn")
			       (bookmark-jump-to-bookmark key t)))
			 #'(lambda ()
			     (when jflg :check))))
	(setq c (1+ c))))
    (add-menu-separator menu)
    (if dmode
	(add-menu-item menu nil "削除モードです")
      (add-menu-item menu nil "しおりをはさむ(&A)"
		     #'(lambda () (interactive) (bookmark-add-new-bookmark t))))
    (let ((smenu (create-popup-menu))
	  (tmenu (create-popup-menu))
	  (orderu  "昇順(&U)")
	  (orderd  "降順(&D)"))
      (add-popup-menu menu smenu (format nil "フルパス名でソート(&F)"))
      (add-menu-item smenu nil orderu
		     #'(lambda () (interactive)
			 (bookmark-sort-bookmark-list-by-filename #'> #'string-greaterp dmode))
		     #'(lambda () sinit))
      (add-menu-item smenu nil orderd
		     #'(lambda () (interactive)
			 (bookmark-sort-bookmark-list-by-filename #'< #'string-lessp dmode))
		     #'(lambda () sinit))
      (add-popup-menu menu tmenu (format nil "更新日時でソート(&T)"))
      (add-menu-item tmenu nil orderu
		     #'(lambda () (interactive)
			 (bookmark-sort-bookmark-list-by-time #'string> dmode))
		     #'(lambda () sinit))
      (add-menu-item tmenu nil orderd
		     #'(lambda () (interactive)
			 (bookmark-sort-bookmark-list-by-time #'string< dmode))
		     #'(lambda () sinit)))
    (add-menu-item menu nil "編集する(&E)" 'bookmark-edit-bookmark-list)
    (add-menu-item menu nil "すべて削除(&C)" 'bookmark-clear-all
		   #'(lambda () minit))
    (if dmode
	(progn (message "削除モード")
	  (add-menu-item menu nil "ジャンプモードにする(&J)"
			 #'(lambda () (interactive) (my-bookmark-menu nil nil))))
      (progn (message "ジャンプモード")
	(add-menu-item menu nil "削除モードにする(&D)"
		       #'(lambda () (interactive) (my-bookmark-menu t nil))
		       #'(lambda () minit))))
    (track-popup-menu menu btn)))

;;; しおりを挟んだ位置に戻る
(defun bookmark-jump-to-previous-bookmark (&optional n)
  (interactive)
  (let ((exis (bookmark-data-read-from-file))
	l fn-or-bn pt jflg tflg fflg key fkey predt)
    (unless exis
      (plain-error "しおりがありません。")
      (return-from bookmark-jump-to-previous-bookmark))
    (setq predt (if n
		    #'string<
		  #'string>))
    (setq l (sort *bookmark-list* predt :key #'cadar))
    (dolist (b l)
      (or n fflg
	  (progn
	    (setq fkey (cdr b))
	    (setq fflg t)))
      (setq key (cdr b))
      (setq fn-or-bn (cadr b))
      (setq pt (cddr b))
      (when tflg
	(bookmark-jump-to-bookmark key nil n)
	(return-from bookmark-jump-to-previous-bookmark))
      (setq jflg (cddar b))
      (when jflg
	(or (and (equal fn-or-bn (get-buffer-file-name))
		 (= pt (point)))
	    (and (equal fn-or-bn (get-buffer-file-name))
		 (eobp)
		 (> pt (point)))
	    (and (equal fn-or-bn (buffer-name (selected-buffer)))
		 (= pt (point)))
	    (and (equal fn-or-bn (buffer-name (selected-buffer)))
		 (eobp)
		 (> pt (point)))
	    (progn
	      (bookmark-jump-to-bookmark key nil n)
	      (return-from bookmark-jump-to-previous-bookmark)))
	(setq tflg t)))
    (if tflg
	(plain-error "ないよん。")
      (progn
	(unless n
	  (setq key fkey))
	(bookmark-jump-to-bookmark key)))
    ))

(defun bookmark-jump-to-next-bookmark ()
  (interactive)
  (bookmark-jump-to-previous-bookmark t))

;;; しおりを保存するファイルの切り替え
(defun bookmark-menu-selected-file (file)
  (setq *bookmark-list-file* file)
  (my-bookmark-menu nil :button2))

(defun bookmark-select-list-file ()
  (interactive)
  (if *bookmark-list-file-list*
      (let ((menu (create-popup-menu)))
	(dolist (l *bookmark-list-file-list*)
	  (let ((menu-item (car l))
		(file (cdr l)))
	    (add-menu-item menu nil menu-item
			   #'(lambda ()
			       (interactive)
			       (bookmark-menu-selected-file file))
			   #'(lambda ()
			       (when (equal file *bookmark-list-file*)
				 :check)))))
	(track-popup-menu menu :button2))
    (plain-error "しおりを保存するファイルのリストが設定されていません。")))

;;; ツールバー
(defun my-bookmark-tool-bar ()
  (create-tool-bar 'my-bookmark-tool-bar
		   (merge-pathnames "toolbar.bmp" (etc-path))
		   '(("しおり"	11 (lambda () (interactive)
				     (my-bookmark-menu nil :button2))))))

(define-command-bar 'my-bookmark-tool-bar "しおり(&S)")

;;; アンインストール
(defun my-bookmark-uninstall ()
  (interactive)
  (unregister-history-variable '*bookmark-list-file*)
  (delete-command-bar 'my-bookmark-tool-bar))

;;; キーバインディング
(global-set-key '(#\C-c #\b #\a) #'(lambda () (interactive) (bookmark-add-new-bookmark nil)))
(global-set-key '(#\C-c #\b #\c) 'bookmark-clear-all)
(global-set-key '(#\C-c #\b #\m) #'(lambda () (interactive) (my-bookmark-menu nil :button2)))
(global-set-key '(#\C-c #\b #\d) #'(lambda () (interactive) (my-bookmark-menu t :button2)))
(global-set-key '(#\C-c #\b #\e) 'bookmark-edit-bookmark-list)
(global-set-key '(#\C-c #\b #\p) 'bookmark-jump-to-previous-bookmark)
(global-set-key '(#\C-c #\b #\n) 'bookmark-jump-to-next-bookmark)
(global-set-key '(#\C-c #\b #\s) 'bookmark-select-list-file)
(global-set-key '(#\S-F2) #'(lambda () (interactive) (my-bookmark-menu nil :button2)))
