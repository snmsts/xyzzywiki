[[POPFile:http://popfile.sourceforge.net/cgi-bin/wiki.pl?JP_POPFileDocumentationProject]]のバケツ分類をKaMailから ~

 (require "xmlrpcm/popfile")
 
 (in-package "kamail")
 
 (defvar *popfile-url* "http://localhost:8081/RPC2")
 (defvar *popfile-username* "admin")
 (defvar *popfile-password* "")
 
 (defvar *popfile-header-classification* "x-text-classification")
 
 (defvar *popfile-last-bucket* nil)
 
 (defun popfile-get-session-key ()
   (xmlrpcm::popfile-get-session-key *popfile-url*
 				    *popfile-username*
 				    *popfile-password*))
 
 (defun kamail-popfile-select-bucket ()
   (interactive)
   (let (num folder header bucket-current file)
     (save-excursion
       (set-buffer *kamail-buffer-view*)
       (setq num kamail-number)
       (setq folder kamail-folder)
       (setq header kamail-header)
       (setq bucket-current (get-header-value *popfile-header-classification*
 					     header))
       (setq file (local-file-name folder (parse-integer num))))
     (when file
       (setq *popfile-last-bucket* nil)
       (let ((sesskey (popfile-get-session-key))
 	    buckets)
 	(unless sesskey
 	  (error "Session Keyを取得できません。"))
 	(unwind-protect
 	    (when (setq buckets (xmlrpcm::popfile-get-buckets *popfile-url*
 							      sesskey))
 	      (let ((menu (create-popup-menu)))
 		(dolist (b buckets)
 		  (let ((bucket b)
 			(n num)
 			(filename file)
 			(cur-p (equal b bucket-current)))
 		    (add-menu-item menu
 				   bucket
 				   bucket
 				   #'(lambda ()
 				       (interactive)
 				       (kamail-popfile-add-message-to-bucket n
 									     sesskey
 									      bucket
 									      filename
 									      bucket-current))
 				   #'(lambda ()
 				       (when cur-p :check)))))
 		(track-popup-menu menu)))
 	  (xmlrpcm::popfile-release-session-key *popfile-url*
 						sesskey)))
       (when *popfile-last-bucket*
 	(message "Bucket set: ~A" *popfile-last-bucket*))
       )))
 
 (defun kamail-popfile-add-message-to-bucket (num sesskey bucket filename &optional bucket-current)
   (interactive)
   (when (equal bucket bucket-current)
     (return-from kamail-popfile-add-message-to-bucket))
   (let ((filepath (map-slash-to-backslash filename))
 	result)
     (unless sesskey
       (error "Session Keyを取得できません。"))
     (setq result
 	  (progn
 	    (and bucket-current
 		 (xmlrpcm::popfile-remove-message-from-bucket *popfile-url*
 							      sesskey
 							      bucket
 							      filepath))
 	    (xmlrpcm::popfile-add-message-to-bucket *popfile-url*
 						    sesskey
 						    bucket
 						    filepath)))
     (if result
 	(setq *popfile-last-bucket* bucket)
       (error "エラーが発生しました。"))))
 
 (define-key *kamail-view-map*      #\z 'kamail-popfile-select-bucket)
 (define-key *kamail-multipart-map* #\z 'kamail-popfile-select-bucket)

----
